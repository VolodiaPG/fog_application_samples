export SSHPASS:="faas"
export SSH_CMD:="sshpass -e ssh -t -oUserKnownHostsFile=/dev/null -oStrictHostKeyChecking=no root@127.0.0.1 -p 2221"
export SCP_CMD:="sshpass -e scp -oUserKnownHostsFile=/dev/null -oStrictHostKeyChecking=no -P 2221"

# Entrypoint, run deploy and then tunnels
_default:
    @just --list

fmt:
    nix --experimental-features "nix-command flakes" fmt .

build target:
    nix build {{target}}

build-vm: (build ".#vm")

update:
    nix flake update --experimental-features "nix-command flakes"

ssh-in:
    @$SSH_CMD

scp-in srcfiles destfiles:
    @$SCP_CMD root@127.0.0.1:{{srcfiles}} {{destfiles}}

upload city="rennes":
    #!/usr/bin/env bash
    set -ex
    temp=./nixos.qcow2
    vm_path=$(nix build --extra-experimental-features nix-command --extra-experimental-features flakes .#vm --print-out-paths --no-link --quiet)/nixos.qcow2
    cp $vm_path $temp
    chmod u+rwx $temp

    qemu-kvm \
        -cpu max \
        -name nixos \
        -m 4096 \
        -smp 4 \
        -drive cache=writeback,file="$temp",id=drive1,if=none,index=1,werror=report -device virtio-blk-pci,drive=drive1 \
        -net nic,netdev=user.0,model=virtio -netdev user,id=user.0,hostfwd=tcp::2221-:22 \
        -enable-kvm \
        -nographic > /dev/null&
    
    until $SSH_CMD "k3s kubectl get pods -A" 2> /dev/null
    do
        sleep 10
    done

    until $SSH_CMD "k3s kubectl wait pods -n openfaas -l app=gateway --for condition=Ready --timeout=120s" 2> /dev/null
    do
        sleep 10
    done

    until $SSH_CMD "k3s kubectl wait pods -n openfaas -l app=prometheus --for condition=Ready --timeout=120s" 2> /dev/null
    do
        sleep 10
    done

    $SSH_CMD "nohup bash -c 'sleep 10 && shutdown 0'"

    wait 

    rsync -chavzP --inplace --stats --perms --chmod=u+rwx,g+rwx,o+rwx "$temp" {{city}}.grid5000.fr:~/nixos.qcow2

_run_vm disk="result/nixos.qcow2":
    #!/usr/bin/env bash
    set -e
    cp {{disk}} nixos.qcow2
    temp=nixos.qcow2
    chmod u+rwx $temp

    qemu-kvm \
        -cpu max \
        -name nixos \
        -m 4096 \
        -smp 4 \
        -drive cache=writeback,file="$temp",id=drive1,if=none,index=1,werror=report -device virtio-blk-pci,drive=drive1 \
        -net nic,netdev=user.0,model=virtio -netdev user,id=user.0,hostfwd=tcp::2221-:22 \
        -enable-kvm \
        -nographic&
    
    wait

_openfaas_in_vm:
    #!/usr/bin/env bash
    set -e

    rm vm.kubeconfig
    
    until $SSH_CMD "k3s kubectl get pods -A" 2> /dev/null
    do
        sleep 2
    done

    until $SSH_CMD "k3s kubectl wait pods -n openfaas -l app=gateway --for condition=Ready --timeout=120s" 2> /dev/null
    do
        sleep 2
    done

    until $SSH_CMD "k3s kubectl wait pods -n openfaas -l app=prometheus --for condition=Ready --timeout=120s" 2> /dev/null
    do
        sleep 2
    done
    
    $SSH_CMD "cat /etc/rancher/k3s/k3s.yaml>&1" > vm.kubeconfig 2>&2

    while ! $SSH_CMD "sh -c 'systemctl is-active --quiet influxdb2'"; do
        sleep 2
    done
    # while ! $SSH_CMD "sh -c 'systemctl is-active --quiet grafana'"; do
    #     sleep 2
    # done

    
    $SSH_CMD "docker run -d --name jaeger -e COLLECTOR_ZIPKIN_HTTP_PORT=9411 -p 5775:5775/udp -p 6831:6831/udp -p 6832:6832/udp -p 5778:5778 -p 16686:16686 -p 14268:14268 -p 9411:9411 jaegertracing/all-in-one:1.51.0"

    $SSH_CMD -N -L 6443:0.0.0.0:6443&
    $SSH_CMD -N -L 8080:0.0.0.0:8080&
    $SSH_CMD -N -L 9090:0.0.0.0:9090&
    $SSH_CMD -N -L 9086:0.0.0.0:9086&
    $SSH_CMD "k3s kubectl port-forward -n openfaas svc/gateway 8080:8080"&
    $SSH_CMD "k3s kubectl port-forward -n openfaas svc/prometheus 9090:9090"&
    $SSH_CMD -N -L 14268:0.0.0.0:14268&
    $SSH_CMD -N -L 16686:0.0.0.0:16686&
    $SSH_CMD -N -L 5775:0.0.0.0:5775&
    $SSH_CMD -N -L 6831:0.0.0.0:6831&
    $SSH_CMD -N -L 6832:0.0.0.0:6832&
    $SSH_CMD -N -L 5778:0.0.0.0:5778&
    wait

vm: build-vm
    mprocs "just _run_vm" "just _openfaas_in_vm"

vm-nocopy:
    mprocs "just _run_vm ./nixos.qcow2" "just _openfaas_in_vm"
    
    
    
    