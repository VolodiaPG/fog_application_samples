DOCKER := "podman"

_default:
    @just --list

# Run the echo server
run $SERVER_PORT="30080":
	cargo run --features jaeger

clippy:
	cargo clippy --fix --allow-staged --allow-dirty  -- -A clippy::let_unit_value -D warnings

# Build the project as a docker image
container *FLAGS:
    {{DOCKER}} build --target iot_emulation-default -t iot_emulation:latest  --build-arg FLAGS="{{FLAGS}}" .

# Push an image to ghcr
_push image user:
    {{DOCKER}} tag {{image}} ghcr.io/{{user}}/{{image}}
    {{DOCKER}} push ghcr.io/{{user}}/{{image}}

# Push docker images to ghcr
ghcr user *FLAGS: (container FLAGS)
    just _push iot_emulation:latest {{user}}

check:
    cargo check
    cargo check --features jaeger