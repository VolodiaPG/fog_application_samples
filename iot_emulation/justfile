DOCKER := "podman"
server_port := env_var_or_default("SERVER_PORT", "30080")

_default:
    @just --list

# Run the echo server
run:
    export SERVER_PORT={{server_port}} && cargo run --features jaeger

interval period='20' enabled='true':
    curl -X POST --url "http://localhost:{{server_port}}/api/interval?enabled={{enabled}}&intervalMs={{period}}"

pre_commit: check clippy
    cargo fmt

clippy:
	cargo clippy --fix --allow-staged --allow-dirty  -- -A clippy::let_unit_value -D warnings

# Build the project as a docker image
container:
    nom build .#iot_emulation
    {{DOCKER}} load < result

# Push an image to ghcr
_push image user:
    {{DOCKER}} tag {{image}} ghcr.io/{{user}}/{{image}}
    {{DOCKER}} push ghcr.io/{{user}}/{{image}}

# Push docker images to ghcr
ghcr user: container
    just _push iot_emulation:latest {{user}}

check:
    cargo check
    cargo check --features jaeger