set dotenv-load

RUN := env_var_or_default("RUN", "python")
DOCKER := env_var_or_default("DOCKER", "podman")
export MANAGER_PATH := "../../manager"
export IOT_EMULATION_PATH := "../../iot_emulation"
export FUNCTIONS_PATH := "../../openfaas-functions"

# Entrypoint, run deploy and then tunnels
_default:
    @just --list

init user *FLAGS:
    #!/usr/bin/env bash
    cat <<EOF > ~/.ssh/config
    Host !access.grid5000.fr *.grid5000.fr
    User {{user}}
    ProxyJump {{user}}@access.grid5000.fr
    StrictHostKeyChecking no
    UserKnownHostsFile /dev/null
    ForwardAgent yes
    EOF

    {{RUN}} integration.py init --g5k_user={{user}} {{FLAGS}}

lab:
    nix run .#jupyterlab

labExport:
    nix run .#jupyterlabExport

# Open SSH tunnels
tunnels +FLAGS='':
    {{RUN}} integration.py tunnels {{FLAGS}}

# List end nodes of the Fog network
endpoints:
	{{RUN}} integration.py endpoints

_is_connected city_name_node_target market_port:
    #!/usr/bin/env bash
    market_ip=$({{RUN}} integration.py market-ip | grep address | sed 's/address: //')
    if [[ $(curl -s "$market_ip:{{market_port}}/api/fog" | jq '.[] | select(.tags[] == "{{city_name_node_target}}") | .id' | sed -nE 's/"(.*)"/\1/p') ]]; then
        echo "✅ {{city_name_node_target}}@$market_ip is connected"
    else
        echo "❌ {{city_name_node_target}}@$market_ip"
    fi

is_connected city_name_node_target market_local_port:
    watch --no-title --color --no-wrap just _is_connected {{city_name_node_target}} {{market_local_port}}

deploy fog_node_image market_image $DEPLOYMENT_NAME:
    {{RUN}} integration.py up --name="$DEPLOYMENT_NAME" --walltime "$DEPLOYMENT_WALLTIME" --force
    just _deploy {{fog_node_image}} {{market_image}}

refresh fog_node_image market_image $DEPLOYMENT_NAME:
    {{RUN}} integration.py restart
    just _deploy {{fog_node_image}} {{market_image}}

# Delete the Job on grid'5000 and local EnosLib files
clean:
    {{RUN}} integration.py clean || true
    rm -rf enos_* current cachedir __enos*   

# Refresh the container images hosted by k3s on all deployed nodes
_deploy fog_node_image market_image:
    #!/usr/bin/env bash
    set -e
    {{RUN}} integration.py network

    # {{RUN}} integration.py monitoring&r
    {{RUN}} integration.py iot-emulation&
    ({{RUN}} integration.py k3s-setup && \
        {{RUN}} integration.py k3s-deploy --fog_node_image={{fog_node_image}} --market_image={{market_image}})&
    
    wait

expe $IOT_IP $MARKET_IP $TARGET_NODES:
    {{RUN}} expe.py

scenario archive_name +city_name_node_targets:
    #!/usr/bin/env bash
    set -ex
    NODE_TARGETS=()
    MARKET_IP=$({{RUN}} integration.py market-ip | grep address | sed 's/address: //')
    for city in {{city_name_node_targets}}; do
        echo "Checking connection to $city"
        until [[ "$(just _is_connected $city $MARKET_LOCAL_PORT)" =~ "✅" ]]; do sleep 10 && echo -e "."; done
        NODE_TARGETS+=($(curl "$MARKET_IP:$MARKET_LOCAL_PORT/api/fog" | jq ".[] | select(.tags[] == \"$city\") | .id" | sed -nE 's/"(.*)"/\1/p'))
    done
    IOT_IP=$(just endpoints | sed -nE 's/Iot emulation IP -> (.*)/\1/p')
    printf -v joined '%s\t' "${NODE_TARGETS[@]}"
    export TARGET_NODE_NAMES="{{city_name_node_targets}}"
    
    just expe $IOT_IP $MARKET_IP "$joined"
    
    sleep $WAIT_TIME

    export COLLECT_ARCHIVE_NAME="{{archive_name}}"
    {{RUN}} integration.py collect || true
    # just logs --all

scenarii +city_name_node_targets:
    #!/usr/bin/env bash
    set -e

    # Function to generate a random number within a range
    get_random_number() {
    local min=$1
    local max=$2
    echo $((RANDOM % (max - min + 1) + min))
    }

    # Function to shuffle the array
    shuffle_array() {
    local array=("$@")
    local array_size=${#array[@]}

    for ((i = 0; i < array_size; i++)); do
        # Generate a random index
        local random_index=$(get_random_number 0 $((array_size - 1)))

        # Swap the current element with the element at the random index
        local temp=${array[i]}
        array[i]=${array[random_index]}
        array[random_index]=$temp
    done

    # Print the shuffled array
    echo "${array[@]}"
    }

    # Convert the space-separated list of fog node image tags to a Bash array
    TAGS=(${FOG_NODE_IMAGE_TAGS// / })

    TAGS=($(shuffle_array "${TAGS[@]}"))

    for INDEX in "${!TAGS[@]}"; do
        TAG="${TAGS[INDEX]}"
        echo "Using image $IMAGE_REGISTRY/$FOG_NODE_IMAGE_NAME:$TAG and $IMAGE_REGISTRY/$MARKET_IMAGE"
        if [ "$INDEX" -eq 0 ]; then
            just deploy "$IMAGE_REGISTRY/$FOG_NODE_IMAGE_NAME:$TAG" "$IMAGE_REGISTRY/$MARKET_IMAGE" $DEPLOYMENT_NAME
        else
            just refresh "$IMAGE_REGISTRY/$FOG_NODE_IMAGE_NAME:$TAG" "$IMAGE_REGISTRY/$MARKET_IMAGE" $DEPLOYMENT_NAME \
            || just deploy "$IMAGE_REGISTRY/$FOG_NODE_IMAGE_NAME:$TAG" "$IMAGE_REGISTRY/$MARKET_IMAGE" $DEPLOYMENT_NAME
        fi

        sleep 60

        just tunnels --command="'just scenario \"$DEPLOYMENT_NAME-$TAG\" {{city_name_node_targets}}'"
    done

    just clean

collect:
    mkdir -p metrics
    {{RUN}} integration.py collect --address $INFLUX_ADDRESS

_collect:
    mkdir -p metrics
    {{RUN}} integration.py collect

logs *FLAGS:
    {{RUN}} integration.py logs {{FLAGS}}

vpn:
    #!/usr/bin/env bash
    set -e
    modprobe tun
    rm -rf grid5000-openvpn || true
    mkdir grid5000-openvpn
    unzip grid5000-openvpn.zip -d grid5000-openvpn
    cd grid5000-openvpn
    cat /root/.python-grid5000.yaml | grep 'password: ' | sed 's/password: //' > grid5000vpnpassword
    cat <<EOF >> Grid5000_VPN.ovpn
    script-security 2
    up /etc/openvpn/update-resolv-conf
    down /etc/openvpn/update-resolv-conf
    askpass "$PWD/grid5000vpnpassword"
    daemon
    EOF
    openvpn Grid5000_VPN.ovpn

sshconfig:
    #!/usr/bin/env bash
    set -e
    login=$(cat /root/.python-grid5000.yaml | grep 'username: ' | sed 's/username: //')
# cat <<EOF > /root/.ssh/config
# Host access.grid5000.fr
#     User $login
#     ControlMaster auto
#     ControlPath /dev/shm/ssh-%h
#     ControlPersist 2h

# Host !access.grid5000.fr *.grid5000.fr
#     User $login
#     ProxyJump access.grid5000.fr
#     StrictHostKeyChecking no
#     UserKnownHostsFile /dev/null
#     ForwardAgent yes
# EOF
# ssh "$login@$CLUSTER.grid5000.fr" -c "sleep 999999"&

docker_enos name tempdir cmd="bash":
    #!/usr/bin/env bash
    set -e
    {{DOCKER}} run -it --rm \
        --privileged \
        --cap-add=ALL \
        --workdir /home/enos \
        -v /lib/modules:/lib/modules \
        -v {{justfile_directory()}}/integration.py:/home/enos/integration.py:ro \
        -v {{justfile_directory()}}/collect.py:/home/enos/collect.py:ro \
        -v {{tempdir}}/definitions.py:/home/enos/definitions.py:ro \
        -v {{justfile_directory()}}/monitoring:/home/enos/monitoring:ro \
        -v {{justfile_directory()}}/k3s.py:/home/enos/k3s.py:ro \
        -v {{justfile_directory()}}/expe.py:/home/enos/expe.py:ro \
        -v {{justfile_directory()}}/justfile:/home/enos/justfile:ro \
        -v {{justfile_directory()}}/metrics-arks:/home/enos/metrics-arks \
        -v {{justfile_directory()}}/logs:/home/enos/logs \
        -v {{tempdir}}/.env:/home/enos/.env:ro \
        -v {{tempdir}}/requests:/home/enos/requests:ro \
        -v $GRID5000OPENVPNZIP:/home/enos/grid5000-openvpn.zip:ro \
        -v ~/.python-grid5000.yaml:/root/.python-grid5000.yaml:ro \
        -v ~/.ssh/id_rsa:/root/.ssh/id_rsa:ro \
        -v ~/.ssh/id_rsa.pub:/root/.ssh/id_rsa.pub:ro \
        -v $(readlink -f $SSH_AUTH_SOCK):/ssh-agent \
        -e SSH_AUTH_SOCK=/ssh-agent \
        -e DEPLOYMENT_NAME="{{name}}" \
        -e EXPE_LOAD_FILE=/home/enos/requests \
        --name "{{name}}" \
        --cgroup-manager=cgroupfs \
        enos_deployment:latest bash -c -E "just sshconfig && {{cmd}}"

# --cgroup-manager=cgroupfs fix needs to be investigated because ... Linux ?!?

docker_scenarii name tempdir +city_name_node_targets:
    echo "Generating requests for all subsequent experiments..."
    EXPE_SAVE_FILE={{tempdir}}/requests TARGET_NODE_NAMES="{{city_name_node_targets}}" {{RUN}} expe.py
    just docker_enos {{name}} {{tempdir}} "(just vpn && just scenarii {{city_name_node_targets}}) || just clean"

_docker_campain name tempdir:
    #!/usr/bin/env bash
    set -e
    cities=()
    for input in $({{RUN}} integration.py iot-connections); do cities[${#cities[@]}]="'$input'"; done
    just docker_scenarii {{name}} {{tempdir}} ${cities[@]}

docker_campain_in_nix suffix dotenvfile:
    #!/usr/bin/env bash
    set -e
    tempdir=$(mktemp -d)
    cp {{dotenvfile}} $tempdir/.env
    cp definitions.py $tempdir/definitions.py
    mkdir -p metrics-arks logs
    # Build image before calling it in parrallel as it confuses podman

    image_path=$(nix build --extra-experimental-features nix-command --extra-experimental-features flakes .#docker --print-out-paths --no-link --quiet)
    {{DOCKER}} load < $image_path

    today=$( date +%Y%m%d )
    number=0
    fname={{suffix}}-$today.log
    while [ -e "$fname" ]; do
        printf -v fname '%s-%02d.log' "{{suffix}}-$today" "$(( ++number ))"
    done

    mkdir -p campaing_logs
    args=()
    for i in $(seq 1 $NB_REPETITIONS); do
        args+=("just _docker_campain {{suffix}}_$i $tempdir 2>&1 | tee campaing_logs/$fname")
    done
    
    mprocs "${args[@]}"

build_required_images user:
    #!/usr/bin/env bash
    set -e
    
    # TAGS=(${FOG_NODE_IMAGE_TAGS// / })
    echo $FOG_NODE_IMAGE_TAGS
    cd $MANAGER_PATH
    nix develop -c just ghcr {{user}} $FOG_NODE_IMAGE_TAGS
    cd {{justfile_directory()}}
    cd $IOT_EMULATION_PATH
    nix develop -c  just ghcr {{user}}
    cd {{justfile_directory()}}
    cd $FUNCTIONS_PATH
    nix develop -c just faas

docker_campain user variation="valuation_rates" dotenvfile=".env": (build_required_images user)
    nix develop --extra-experimental-features "nix-command flakes" -c just docker_campain_in_nix "{{variation}}{{dotenvfile}}" {{dotenvfile}}

dev:
    nix develop --extra-experimental-features nix-command --extra-experimental-features flakes

sim methods="auction edge_ward edge_first" pricing="constant linear same" $RANDOM_SEED="":
    #!/usr/bin/env bash
    export cities=()
    for input in $({{RUN}} integration.py iot-connections); do cities[${#cities[@]}]="'$input'"; done
    export EXPE_SAVE_FILE=./requests 
    export TARGET_NODE_NAMES="${cities[@]}"
    {{RUN}} expe.py
    # methods=(${ {{methods}}// / })
    # pricing=(${ {{pricing}}// / })
    # echo $methods
    parallel --keep-order PLACEMENT_STRATEGY={1} PRICING_STRATEGY={2} {{RUN}} simulator.py ::: {{methods}} ::: {{pricing}}

simple_sim $PLACEMENT_STRATEGY $PRICING_STRATEGY:
    #!/usr/bin/env bash
    export cities=()
    for input in $({{RUN}} integration.py iot-connections); do cities[${#cities[@]}]="'$input'"; done
    export EXPE_SAVE_FILE=./requests 
    export TARGET_NODE_NAMES="${cities[@]}"
    export RANDOM_SEED=42
    [[ -f $EXPE_SAVE_FILE ]] || {{RUN}} expe.py
    {{RUN}} simulator.py